// Any copyright is dedicated to the Public Domain.
// http://creativecommons.org/publicdomain/zero/1.0/


field f: Int

method t1(n: Int) returns ()
    requires n > 0
{
    var i: Int := 0

    while (i < n)
        invariant i <= n
    {
        i := i+1
    }
}

method t2(n: Int) returns ()
    requires n > 0
{
    var i: Int := 0

    while (i < n)
        //:: ExpectedOutput(invariant.not.preserved:assertion.false)
        invariant i < n
    {
        i := i+1
    }
}

method t3(n: Int) returns ()
    requires n > 0
{
    var i: Int := 2

    while (i < n)
        //:: ExpectedOutput(invariant.not.established:assertion.false)
        invariant i <= n
    {
        i := i+1
    }
}

method t4(n: Int, r: Ref) returns ()
    requires n > 0
    requires r != null
{
    var i: Int := 2

    //:: ExpectedOutput(while.failed:insufficient.permission)
    while (i < (r.f))
    {
        i := i+1
    }
}

method t5(n: Int, r: Ref) returns ()
    requires n > 0
    requires acc(r.f, write)
{
    var i: Int := 2

    while (i < (r.f))
        invariant acc(r.f, write)
    {
        i := i+1
    }
}

method t6(r: Ref) returns ()
{
    while (perm(r.f) == none) /* OK - perm(e.f) does not require e != null */
    {
    }
}

method t7(r: Ref) returns ()
{
    var i: Int := 1
    var j: Int := 2

    while (true)
    {
        assert i == 1
        //:: ExpectedOutput(assert.failed:assertion.false)
        assert j == 2
        j := j+1
    }
}

method t8(r: Ref) returns ()
{
    var j: Int := 2

    while (j < 10)
    {
        j := j+1
    }

    assert j >= 10
    //:: ExpectedOutput(assert.failed:assertion.false)
    assert false
}

method t9(r: Ref) returns ()
    requires acc(r.f, write)
{
    var j: Int := 2
    while (j < (r.f))
        invariant acc(r.f, write)
    {
        j := j+1
    }
    assert j >= (r.f)
}

method t10(r: Ref) returns ()
    requires acc(r.f, write)
{
    var j: Int := 2

    //:: ExpectedOutput(while.failed:insufficient.permission)
    while (j < (r.f))
    {
        j := j+1
    }
    assert j >= (r.f)
}

method t11(r: Ref) returns ()
  requires r != null
{
    var j: Int := 2

    while (j < 3)
        //:: ExpectedOutput(not.wellformed:insufficient.permission)
        //:: ExpectedOutput(invariant.not.established:assertion.false)
        //:: MissingOutput(invariant.not.established:assertion.false, /Silicon/issue/34/)
        invariant r != null && r.f == 2
    {
        j := j+1
    }
}

//APPENDED PERMISSION MODEL

domain _Perm {
    function unit(): Perm 
    function full(): Perm
    function plus(x: Perm, y: Perm): Perm
    function minus(x: Perm, y: Perm): Perm
    function geq(x: Perm, y: Perm): Bool
    function joinable(x: Perm, y: Perm): Bool

    function times(x: Perm, y: Perm): Perm

    //Auxiliary functions
    function share(x: Rational): Perm
    function valid_share(x: Rational): Bool
    function biConstr(Int, Int): Perm

    //Axioms
    axiom axBiConstr {forall a: Int, b: Int :: {biConstr(a, b)} biConstr(a, b) == share(a/b)}


    axiom axValid { forall x: Rational :: {valid_share(x)}
        valid_share(x) <==> (x >= 0/1 && x <= 1/1)
    }
    axiom axUnit { unit() == share(0/1) }
    axiom axFull { full() == share(1/1) }
    axiom axInj { forall a: Rational, b: Rational :: { share(a), share(b) }
        share(a) == share(b) ==> a == b
    }
    axiom axPlus { forall a: Rational, b: Rational :: { plus(share(a), share(b)) }
        joinable(share(a), share(b)) ==> plus(share(a), share(b)) == share(a + b)
    }
    axiom axMinus { forall a: Rational, b: Rational :: { minus(share(a), share(b)) }
        geq(share(a), share(b)) ==> minus(share(a), share(b)) == share(a - b)
    }
    axiom axCanJoin { forall a: Rational, b: Rational :: { joinable(share(a), share(b)) }
        joinable(share(a), share(b)) <==> valid_share(a) && valid_share(b) && valid_share(a + b)
    }
    axiom axGeq { forall a: Rational, b: Rational :: { geq(share(a), share(b)) }
        geq(share(a), share(b)) <==> valid_share(a) && valid_share(b) && valid_share(a - b)
    }
    axiom axMul { forall a: Rational, b: Rational :: {times(share(a), share(b))} times(share(a), share(b)) == share(a * b)}

    axiom axJoinComm {forall p: Perm, q: Perm :: {plus(p, q)} plus(p, q) == plus(q, p)}
    axiom axJoinAssoc {forall p: Perm, q: Perm, r: Perm :: {plus(plus(p, q), r)} plus(plus(p, q), r) == plus(p, plus(q, r))}
    axiom axJoinCancel { forall p : Perm, q: Perm, x: Perm :: {plus(p, x), plus(q, x)} plus(p, x) == plus(q, x) ==> p == q}

    axiom axUnitGeq { forall p: Perm :: {geq(p, unit())} geq(p, unit())}
    axiom axUnitGeq2 { forall p: Perm :: {geq(unit(), p)} geq(unit(), p) <==> p == unit()}
    axiom axFullGeq { forall p: Perm :: {geq(full(), p)} geq(full(), p)}
    axiom axFullGeq2 { forall p: Perm :: {geq(p, full())} geq(p, full()) <==> p == full()}

    axiom axUnitJoin { forall p: Perm :: {plus(unit(), p)} plus(unit(), p) == p}
    axiom axTimes0 { forall p: Perm :: {times(unit(), p)} times(unit(), p) == unit()}
    axiom axTimes1 { forall p: Perm :: {times(full(), p)} times(unit(), p) == p}
}
