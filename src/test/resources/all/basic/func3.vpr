// Any copyright is dedicated to the Public Domain.
// http://creativecommons.org/publicdomain/zero/1.0/


field f: Int
field fr: Ref

function f1(r: Ref): Int
    requires acc(r.f, wildcard)
    ensures result == (r.f)
{
    r.f
}

function f2(r: Ref): Int
    requires acc(r.f, wildcard)
    //:: ExpectedOutput(postcondition.violated:assertion.false)
    ensures result == 2
{
    r.f
}

function f3(r: Ref): Int
    requires acc(r.f, wildcard)
    requires (r.f) == 2
    ensures result == 2
{
    r.f
}

function f4(r: Ref): Ref
    requires acc(r.fr, wildcard)
{ r.fr }

method t1(r: Ref)
    requires acc(r.f, write)
{
    r.f := 2
    assert f3(r) == 2
    //:: ExpectedOutput(assert.failed:assertion.false)
    assert f3(r) == 1
}

/* [2013-11-27 Malte]
 *   I removed this test case because in my opinion, it asserts a behaviour
 *   that should be unspecified. Function f2 is not well-formed, thus it is
 *   not clear why f2(r) == 2 should hold. I also wouldn't require that the
 *   assertion should fail, I think it should be unspecified and left up to
 *   the tool whether it accepts the assertion or not (provided, that f2 is
 *   correctly rejected as being ill-formed).
 */
//method t2(r: Ref)
//    requires acc(r.f, write)
//{
//    assert f2(r) == 2
//}

method t3(r: Ref)
    requires acc(r.fr, wildcard)
{
    var y: Ref := f4(r)
    var x: Ref
    x := new()
    assert x != y
}

//APPENDED PERMISSION MODEL

domain _Perm {
    function unit(): Perm 
    function full(): Perm
    function plus(x: Perm, y: Perm): Perm
    function minus(x: Perm, y: Perm): Perm
    function geq(x: Perm, y: Perm): Bool
    function joinable(x: Perm, y: Perm): Bool

    function times(x: Perm, y: Perm): Perm

    //Auxiliary functions
    function share(x: Rational): Perm
    function valid_share(x: Rational): Bool
    function biConstr(Int, Int): Perm

    //Axioms
    axiom axBiConstr {forall a: Int, b: Int :: {biConstr(a, b)} biConstr(a, b) == share(a/b)}


    axiom axValid { forall x: Rational :: {valid_share(x)}
        valid_share(x) <==> (x >= 0/1 && x <= 1/1)
    }
    axiom axUnit { unit() == share(0/1) }
    axiom axFull { full() == share(1/1) }
    axiom axInj { forall a: Rational, b: Rational :: { share(a), share(b) }
        share(a) == share(b) ==> a == b
    }
    axiom axPlus { forall a: Rational, b: Rational :: { plus(share(a), share(b)) }
        joinable(share(a), share(b)) ==> plus(share(a), share(b)) == share(a + b)
    }
    axiom axMinus { forall a: Rational, b: Rational :: { minus(share(a), share(b)) }
        geq(share(a), share(b)) ==> minus(share(a), share(b)) == share(a - b)
    }
    axiom axCanJoin { forall a: Rational, b: Rational :: { joinable(share(a), share(b)) }
        joinable(share(a), share(b)) <==> valid_share(a) && valid_share(b) && valid_share(a + b)
    }
    axiom axGeq { forall a: Rational, b: Rational :: { geq(share(a), share(b)) }
        geq(share(a), share(b)) <==> valid_share(a) && valid_share(b) && valid_share(a - b)
    }
    axiom axMul { forall a: Rational, b: Rational :: {times(share(a), share(b))} times(share(a), share(b)) == share(a * b)}

    axiom axJoinComm {forall p: Perm, q: Perm :: {plus(p, q)} plus(p, q) == plus(q, p)}
    axiom axJoinAssoc {forall p: Perm, q: Perm, r: Perm :: {plus(plus(p, q), r)} plus(plus(p, q), r) == plus(p, plus(q, r))}
    axiom axJoinCancel { forall p : Perm, q: Perm, x: Perm :: {plus(p, x), plus(q, x)} plus(p, x) == plus(q, x) ==> p == q}

    axiom axUnitGeq { forall p: Perm :: {geq(p, unit())} geq(p, unit())}
    axiom axUnitGeq2 { forall p: Perm :: {geq(unit(), p)} geq(unit(), p) <==> p == unit()}
    axiom axFullGeq { forall p: Perm :: {geq(full(), p)} geq(full(), p)}
    axiom axFullGeq2 { forall p: Perm :: {geq(p, full())} geq(p, full()) <==> p == full()}

    axiom axUnitJoin { forall p: Perm :: {plus(unit(), p)} plus(unit(), p) == p}
    axiom axTimes0 { forall p: Perm :: {times(unit(), p)} times(unit(), p) == unit()}
    axiom axTimes1 { forall p: Perm :: {times(full(), p)} times(unit(), p) == p}
}
