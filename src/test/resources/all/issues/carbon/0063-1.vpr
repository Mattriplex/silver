// Any copyright is dedicated to the Public Domain.
// http://creativecommons.org/publicdomain/zero/1.0/

// oldC2SCases/LinkedList-NoFunctions.chalice
/*
It seems that the problem is caused by short-circuit evaluation of `&&`
operator when doing definedness check of the exhale.
`checkDefinednessImpl` function in the file
`src/main/scala/viper/carbon/modules/impls/DefaultExpModule.scala`:

1.  checks the left hand side for well-definedness
2.  converts left hand side to pure expression
3.  if purified left hand side may be true, then checks the
    well-definedness of the right hand side.

The problem is that purification of unfolding expression is not defined. It cannot
be simply translated to true, because of short-circuit evaluation:

  // Here the error should be assert false, and if we replaced unfolding
  // with true, we would get insufficient permission.
  exhale (unfolding acc(P(this), write) in false) && this.x == 0

*/
predicate P(this: Ref) {
  true
}

field x: Int

method test0(this: Ref)
  requires acc(P(this), write)
  //:: ExpectedOutput(not.wellformed:insufficient.permission)
  //:: ExpectedOutput(postcondition.violated:assertion.false)
  //:: MissingOutput(postcondition.violated:assertion.false, /Silicon/issue/34/)
  ensures (unfolding acc(P(this), write) in true) && this.x == 0
{
}

method test1(this: Ref)
{
  inhale acc(P(this), write) && this != null
  //:: ExpectedOutput(exhale.failed:insufficient.permission)
  exhale (unfolding acc(P(this), write) in true) && this.x == 0
}

method test1Reference(this: Ref)
{
  inhale this != null
  //:: ExpectedOutput(exhale.failed:insufficient.permission)
  exhale true && this.x == 0
}

method test2(this: Ref)
{
  inhale acc(P(this), write)
  //:: ExpectedOutput(exhale.failed:assertion.false)
  exhale (unfolding acc(P(this), write) in false) && this.x == 0
}

method test2Reference(this: Ref)
{
  inhale this != null
  //:: ExpectedOutput(exhale.failed:assertion.false)
  exhale false && this.x == 0
}

field y: Int
predicate V(this: Ref) {
  acc(this.y, write) && this.y == 1
}

method test3(this: Ref)
{
  inhale acc(V(this), write)
  //:: ExpectedOutput(exhale.failed:assertion.false)
  exhale (unfolding acc(V(this), write) in this.y == 2) && this.x == 0
}

method test4(this: Ref)
{
  inhale acc(V(this), write)
  //:: ExpectedOutput(exhale.failed:assertion.false)
  exhale (unfolding acc(V(this), write) in this.y) == 2 && this.x == 0
}

//APPENDED PERMISSION MODEL

domain _Perm {
    function unit(): Perm 
    function full(): Perm
    function plus(x: Perm, y: Perm): Perm
    function minus(x: Perm, y: Perm): Perm
    function geq(x: Perm, y: Perm): Bool
    function joinable(x: Perm, y: Perm): Bool

    function times(x: Perm, y: Perm): Perm

    //Auxiliary functions
    function share(x: Rational): Perm
    function valid_share(x: Rational): Bool
    function biConstr(Int, Int): Perm

    //Axioms
    axiom axBiConstr {forall a: Int, b: Int :: {biConstr(a, b)} biConstr(a, b) == share(a/b)}


    axiom axValid { forall x: Rational :: {valid_share(x)}
        valid_share(x) <==> (x >= 0/1 && x <= 1/1)
    }
    axiom axUnit { unit() == share(0/1) }
    axiom axFull { full() == share(1/1) }
    axiom axInj { forall a: Rational, b: Rational :: { share(a), share(b) }
        share(a) == share(b) ==> a == b
    }
    axiom axPlus { forall a: Rational, b: Rational :: { plus(share(a), share(b)) }
        joinable(share(a), share(b)) ==> plus(share(a), share(b)) == share(a + b)
    }
    axiom axMinus { forall a: Rational, b: Rational :: { minus(share(a), share(b)) }
        geq(share(a), share(b)) ==> minus(share(a), share(b)) == share(a - b)
    }
    axiom axCanJoin { forall a: Rational, b: Rational :: { joinable(share(a), share(b)) }
        joinable(share(a), share(b)) <==> valid_share(a) && valid_share(b) && valid_share(a + b)
    }
    axiom axGeq { forall a: Rational, b: Rational :: { geq(share(a), share(b)) }
        geq(share(a), share(b)) <==> valid_share(a) && valid_share(b) && valid_share(a - b)
    }
    axiom axMul { forall a: Rational, b: Rational :: {times(share(a), share(b))} times(share(a), share(b)) == share(a * b)}

    axiom axJoinComm {forall p: Perm, q: Perm :: {plus(p, q)} plus(p, q) == plus(q, p)}
    axiom axJoinAssoc {forall p: Perm, q: Perm, r: Perm :: {plus(plus(p, q), r)} plus(plus(p, q), r) == plus(p, plus(q, r))}
    axiom axJoinCancel { forall p : Perm, q: Perm, x: Perm :: {plus(p, x), plus(q, x)} plus(p, x) == plus(q, x) ==> p == q}

    axiom axUnitGeq { forall p: Perm :: {geq(p, unit())} geq(p, unit())}
    axiom axUnitGeq2 { forall p: Perm :: {geq(unit(), p)} geq(unit(), p) <==> p == unit()}
    axiom axFullGeq { forall p: Perm :: {geq(full(), p)} geq(full(), p)}
    axiom axFullGeq2 { forall p: Perm :: {geq(p, full())} geq(p, full()) <==> p == full()}

    axiom axUnitJoin { forall p: Perm :: {plus(unit(), p)} plus(unit(), p) == p}
    axiom axUnitJoinable { forall p: Perm :: {joinable(p, unit())} joinable(p, unit())}
    axiom axJoinableComm { forall p: Perm, q: Perm :: {joinable(p, q)} joinable(p, q) == joinable(q, p)}
    axiom axTimes0 { forall p: Perm :: {times(unit(), p)} times(unit(), p) == unit()}
    axiom axTimes1 { forall p: Perm :: {times(full(), p)} times(unit(), p) == p}

    axiom axGeqPlus { forall a: Perm, x: Perm, y: Perm :: {geq(a, plus(x, y))} geq(a, plus(x, y)) ==> geq(a, x)}
    axiom axGeqMinus {forall a: Perm, x: Perm, y: Perm :: {geq(a, plus(x, y))} geq(a, plus(x, y)) ==> geq(minus(a, y), x)}
    axiom axMinusUneq { forall p: Perm, q: Perm :: {geq(p, q)} geq(p, q) && p != q ==> minus(p, q) != unit()}

}
