// Any copyright is dedicated to the Public Domain.
// http://creativecommons.org/publicdomain/zero/1.0/

field f: Int
field g: Int

method test01(x: Ref, b: Bool)
  requires acc(x.f, b ? write : none)
{
  assert b || forperm y: Ref [y.f] :: false

  assert (forperm y: Ref [y.f] :: false) ==> (forperm y: Ref [y.f] :: true)
  assert (forperm y: Ref [y.f] :: false) ==> !b

  //:: ExpectedOutput(assert.failed:assertion.false)
  assert forperm y: Ref [y.f] :: false
}

method test01b(x: Ref, b: Bool)
  requires b ==> acc(x.f)
{
  assert b || forperm y: Ref [y.f] :: false

  assert (forperm y: Ref [y.f] :: false) ==> (forperm y: Ref [y.f] :: true)

  assert (forperm y: Ref [y.f] :: false) ==> !b

  //:: ExpectedOutput(assert.failed:assertion.false)
  assert forperm y: Ref [y.f] :: false
}

method test02(x: Ref, b1: Bool, b2: Bool)
  requires acc(x.f, 1/5)
{
  assert forperm y: Ref [y.f] :: y != null
  assert forperm y: Ref [y.g] :: y != null
}

method test03(x: Ref, b1: Bool, b2: Bool)
  requires acc(x.f, 1/5)
{
  var a1: Int := x.f

  inhale acc(x.f, 4/5)
  x.f := a1 + 1

  assert forperm y: Ref [y.g] :: y.f > a1

  //:: ExpectedOutput(assert.failed:insufficient.permission)
  assert forperm y: Ref [y.f] :: y.g > y.g - 1
}

method test04(x: Ref, b1: Bool, b2: Bool)
  requires acc(x.f, 1/5)
{
  var a1: Int := x.f

  inhale acc(x.f, 4/5)
  x.f := a1 + 1

  assert forperm y: Ref [y.f] :: y.f > a1

  assert forperm y: Ref [y.f] :: y.f > old(y.f)

  assert forperm y: Ref [y.f] :: old(y.f) <= old(y.f)
  assert forperm y: Ref [y.g] :: old(y.f) <= old(y.f)
}


//APPENDED PERMISSION MODEL

domain _Perm {
    function unit(): Perm 
    function full(): Perm
    function plus(x: Perm, y: Perm): Perm
    function minus(x: Perm, y: Perm): Perm
    function geq(x: Perm, y: Perm): Bool
    function joinable(x: Perm, y: Perm): Bool

    function times(x: Perm, y: Perm): Perm

    //Auxiliary functions
    function share(x: Rational): Perm
    function valid_share(x: Rational): Bool
    function biConstr(Int, Int): Perm

    //Axioms
    axiom axBiConstr {forall a: Int, b: Int :: {biConstr(a, b)} biConstr(a, b) == share(a/b)}


    axiom axValid { forall x: Rational :: {valid_share(x)}
        valid_share(x) <==> (x >= 0/1 && x <= 1/1)
    }
    axiom axUnit { unit() == share(0/1) }
    axiom axFull { full() == share(1/1) }
    axiom axInj { forall a: Rational, b: Rational :: { share(a), share(b) }
        share(a) == share(b) ==> a == b
    }
    axiom axPlus { forall a: Rational, b: Rational :: { plus(share(a), share(b)) }
        joinable(share(a), share(b)) ==> plus(share(a), share(b)) == share(a + b)
    }
    axiom axMinus { forall a: Rational, b: Rational :: { minus(share(a), share(b)) }
        geq(share(a), share(b)) ==> minus(share(a), share(b)) == share(a - b)
    }
    axiom axCanJoin { forall a: Rational, b: Rational :: { joinable(share(a), share(b)) }
        joinable(share(a), share(b)) <==> valid_share(a) && valid_share(b) && valid_share(a + b)
    }
    axiom axGeq { forall a: Rational, b: Rational :: { geq(share(a), share(b)) }
        geq(share(a), share(b)) <==> valid_share(a) && valid_share(b) && valid_share(a - b)
    }
    axiom axMul { forall a: Rational, b: Rational :: {times(share(a), share(b))} times(share(a), share(b)) == share(a * b)}

    axiom axJoinComm {forall p: Perm, q: Perm :: {plus(p, q)} plus(p, q) == plus(q, p)}
    axiom axJoinAssoc {forall p: Perm, q: Perm, r: Perm :: {plus(plus(p, q), r)} plus(plus(p, q), r) == plus(p, plus(q, r))}
    axiom axJoinCancel { forall p : Perm, q: Perm, x: Perm :: {plus(p, x), plus(q, x)} plus(p, x) == plus(q, x) ==> p == q}

    axiom axUnitGeq { forall p: Perm :: {geq(p, unit())} geq(p, unit())}
    axiom axUnitGeq2 { forall p: Perm :: {geq(unit(), p)} geq(unit(), p) <==> p == unit()}
    axiom axFullGeq { forall p: Perm :: {geq(full(), p)} geq(full(), p)}
    axiom axFullGeq2 { forall p: Perm :: {geq(p, full())} geq(p, full()) <==> p == full()}

    axiom axUnitJoin { forall p: Perm :: {plus(unit(), p)} plus(unit(), p) == p}
    axiom axUnitJoinable { forall p: Perm :: {joinable(p, unit())} joinable(p, unit())}
    axiom axJoinableComm { forall p: Perm, q: Perm :: {joinable(p, q)} joinable(p, q) == joinable(q, p)}
    axiom axTimes0 { forall p: Perm :: {times(unit(), p)} times(unit(), p) == unit()}
    axiom axTimes1 { forall p: Perm :: {times(full(), p)} times(unit(), p) == p}

    axiom axGeqPlus { forall a: Perm, x: Perm, y: Perm :: {geq(a, plus(x, y))} geq(a, plus(x, y)) ==> geq(a, x)}
    axiom axGeqMinus {forall a: Perm, x: Perm, y: Perm :: {geq(a, plus(x, y))} geq(a, plus(x, y)) && joinable(x, y) <==> geq(minus(a, y), x) && geq(a, y)}

}
