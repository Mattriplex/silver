// Any copyright is dedicated to the Public Domain.
// http://creativecommons.org/publicdomain/zero/1.0/

// class Cell
field value:Int

// class ArrayOfInt
field array:Seq[Ref] // containing Cell(s)

method parallel_mergesort(a:Ref, b:Ref, start:Int, end:Int)
  requires acc(a.array, wildcard)
  requires acc(b.array, wildcard)
  requires start >= 0 && end <= |a.array| && |a.array| == |b.array|
  requires forall i:Int :: i in [start..end) ==> acc(a.array[i].value, write)
  requires forall i:Int :: i in [start..end) ==> acc(b.array[i].value, write)
  ensures acc(a.array, wildcard)
  ensures acc(b.array, wildcard)
  ensures start >= 0 && end <= |a.array| && |a.array| == |b.array|
  ensures forall i:Int :: i in [start..end) ==> acc(a.array[i].value, write)
  ensures forall i:Int :: i in [start..end) ==> acc(b.array[i].value, write)
  ensures a.array == old(a.array)
  ensures b.array == old(b.array)
{
    var middle: Int
	var t1A: Seq[Ref]
	var t1B: Seq[Ref]
	var t2A: Seq[Ref]
	var t2B: Seq[Ref]

    if(end-start > 1)
    {
        middle := start + (end-start)\2

		// fork t1 := parallel_mergesort(a,b, start, middle)
		t1A := a.array
		t1B := b.array
		exhale acc(a.array, wildcard)
        exhale acc(b.array, wildcard)
        exhale start >= 0 && middle <= |a.array| && |a.array| == |b.array|
        exhale forall i:Int :: i in [start..middle) ==> acc(a.array[i].value, write)
        exhale forall i:Int :: i in [start..middle) ==> acc(b.array[i].value, write)

		// fork t2 := parallel_mergesort(a,b, middle, end)
		t2A := a.array
		t2B := b.array
		exhale acc(a.array, wildcard)
        exhale acc(b.array, wildcard)
        exhale middle >= 0 && end <= |a.array| && |a.array| == |b.array|
        exhale forall i:Int :: i in [middle..end) ==> acc(a.array[i].value, write)
        exhale forall i:Int :: i in [middle..end) ==> acc(b.array[i].value, write)

		// join t1
		inhale acc(a.array, wildcard)
        inhale acc(b.array, wildcard)
        inhale forall i:Int :: i in [start..middle) ==> acc(a.array[i].value, write)
        inhale forall i:Int :: i in [start..middle) ==> acc(b.array[i].value, write)
        inhale a.array == t1A
        inhale b.array == t1B

		//join t2
	    inhale acc(a.array, wildcard)
        inhale acc(b.array, wildcard)
        inhale forall i:Int :: i in [middle..end) ==> acc(a.array[i].value, write)
        inhale forall i:Int :: i in [middle..end) ==> acc(b.array[i].value, write)
        inhale a.array == t2A
        inhale b.array == t2B

        merge(a,b,start,middle,end)
    }
}

method merge(a: Ref, b:Ref, start:Int, middle:Int, end:Int)
  requires acc(a.array, wildcard)
  requires acc(b.array, wildcard)
  requires 0 <= start && end <= |a.array| && |b.array| == |a.array| && start < middle && middle < end
  requires forall k:Int :: k in [start..end) ==> acc(a.array[k].value, write)
  requires forall l:Int :: l in [start..end) ==> acc(b.array[l].value, write)
  ensures acc(a.array, wildcard)
  ensures acc(b.array, wildcard)
  ensures a.array == old(a.array)
  ensures b.array == old(b.array)
  ensures forall i:Int :: i in [start..end) ==> acc(a.array[i].value, write)
  ensures forall i:Int :: i in [start..end) ==> acc(b.array[i].value, write)
{
    var I:Int := start
    var J:Int := middle
    var N:Int := start

    while(N < end)
      invariant acc(a.array, wildcard)
      invariant acc(b.array, wildcard)
      invariant 0 <= start && end <= |a.array|
      invariant N == I+J-middle && 0 <= start
      invariant start <= I
      invariant middle <= J
      invariant J <= end
      invariant |a.array| == |b.array|
      invariant a.array == old(a.array)
	  invariant b.array == old(b.array)
      invariant forall i:Int :: i in [start..end) ==> acc(a.array[i].value, write)
	  invariant forall i:Int :: i in [start..end) ==> acc(b.array[i].value, write)
    {
        if(J == end || (a.array[I]).value < (a.array[J]).value)
        {
            (b.array[N]).value := (a.array[I]).value
            I:=I+1
        } else
        {
            (b.array[N]).value := (a.array[J]).value
            J:=J+1
        }
        N:=N+1
    }
}


//APPENDED PERMISSION MODEL

domain _Perm {
    function unit(): Perm 
    function full(): Perm
    function plus(_x: Perm, _y: Perm): Perm
    function minus(_x: Perm, _y: Perm): Perm
    function geq(_x: Perm, _y: Perm): Bool
    function joinable(_x: Perm, _y: Perm): Bool

    function times(_x: Perm, _y: Perm): Perm

    //Auxiliary functions
    function share(_x: Rational): Perm
    function valid_share(_x: Rational): Bool
    function biConstr(Int, Int): Perm

    //Axioms
    axiom axBiConstr {forall _a: Int, _b: Int :: {biConstr(_a, _b)} biConstr(_a, _b) == share(_a/_b)}


    axiom axValid { forall _x: Rational :: {valid_share(_x)}
        valid_share(_x) <==> (_x >= 0/1 && _x <= 1/1)
    }
    axiom axUnit { unit() == share(0/1) }
    axiom axFull { full() == share(1/1) }
    axiom axInj { forall _a: Rational, _b: Rational :: { share(_a), share(_b) }
        share(_a) == share(_b) ==> _a == _b
    }
    axiom axPlus { forall _a: Rational, _b: Rational :: { plus(share(_a), share(_b)) }
        joinable(share(_a), share(_b)) ==> plus(share(_a), share(_b)) == share(_a + _b)
    }
    axiom axMinus { forall _a: Rational, _b: Rational :: { minus(share(_a), share(_b)) }
        geq(share(_a), share(_b)) ==> minus(share(_a), share(_b)) == share(_a - _b)
    }
    axiom axCanJoin { forall _a: Rational, _b: Rational :: { joinable(share(_a), share(_b)) }
        joinable(share(_a), share(_b)) <==> valid_share(_a) && valid_share(_b) && valid_share(_a + _b)
    }
    axiom axGeq { forall _a: Rational, _b: Rational :: { geq(share(_a), share(_b)) }
        geq(share(_a), share(_b)) <==> valid_share(_a) && valid_share(_b) && valid_share(_a - _b)
    }
    axiom axMul { forall _a: Rational, _b: Rational :: {times(share(_a), share(_b))} times(share(_a), share(_b)) == share(_a * _b)}

    axiom axJoinComm {forall _p: Perm, _q: Perm :: {plus(_p, _q)} plus(_p, _q) == plus(_q, _p)}
    axiom axJoinAssoc {forall _p: Perm, _q: Perm, _r: Perm :: {plus(plus(_p, _q), _r)} plus(plus(_p, _q), _r) == plus(_p, plus(_q, _r))}
    axiom axJoinCancel { forall _p: Perm, _q: Perm, _x: Perm :: {plus(_p, _x), plus(_q, _x)} plus(_p, _x) == plus(_q, _x) ==> _p == _q}

    axiom axUnitGeq { forall _p: Perm :: {geq(_p, unit())} geq(_p, unit())}
    axiom axUnitGeq2 { forall _p: Perm :: {geq(unit(),_p)} geq(unit(),_p) <==> _p == unit()}
    axiom axFullGeq { forall _p: Perm :: {geq(full(),_p)} geq(full(),_p)}
    axiom axFullGeq2 { forall _p: Perm :: {geq(_p, full())} geq(_p, full()) <==> _p == full()}

    axiom axUnitJoin { forall _p: Perm :: {plus(unit(),_p)} plus(unit(),_p) == _p}
    axiom axUnitJoinable { forall _p: Perm :: {joinable(_p, unit())} joinable(_p, unit())}
    axiom axJoinableComm { forall _p: Perm, _q: Perm :: {joinable(_p, _q)} joinable(_p, _q) == joinable(_q,_p)}
    axiom axTimes0 { forall _p: Perm :: {times(unit(),_p)} times(unit(),_p) == unit()}
    axiom axTimes1 { forall _p: Perm :: {times(full(),_p)} times(unit(),_p) == _p}

    axiom axGeqPlus { forall _a: Perm, _x: Perm, _y: Perm :: {geq(_a, plus(_x, _y))} geq(_a, plus(_x, _y)) ==> geq(_a, _x)}
    axiom axGeqMinus {forall _a: Perm, _x: Perm, _y: Perm :: {geq(_a, plus(_x, _y))} geq(_a, plus(_x, _y)) ==> geq(minus(_a, _y), _x)}
    axiom axMinusUneq { forall _p: Perm, _q: Perm :: {geq(_p, _q)} geq(_p, _q) && _p != _q ==> minus(_p, _q) != unit()}

}
