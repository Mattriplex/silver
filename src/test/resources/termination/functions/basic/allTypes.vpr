// Any copyright is dedicated to the Public Domain.
// http://creativecommons.org/publicdomain/zero/1.0/

import <decreases/all.vpr>

//Example decreasing Int
function fact(x:Int): Int
decreases x
requires x>=0
{
    x==0 ? 1 : x*fact(x-1)
}

function fact_e(x:Int): Int
decreases x
requires x>=0
{
    //:: ExpectedOutput(termination.failed:tuple.false)
    x==0 ? 1 : x*fact_e(x)
}

//Example decreasing Reference
function references(r:Ref):Int
decreases r
{
    r == null ? 1 : references(null)
}

function references_e(r:Ref):Int
decreases r
{
    //:: ExpectedOutput(termination.failed:tuple.false)
    r == null ? 1 : references_e(r)
}


//Example decreasing Booleans
function booleans(b: Bool): Int
decreases b
ensures result == 1
{
    b ? booleans(!b) : 1
}

function booleans_e(b: Bool): Int
decreases b
ensures result == 1
{
    //:: ExpectedOutput(termination.failed:tuple.false)
    !b ? booleans_e(!b) : 1
}

function rationals(r: Rational): Rational
decreases r
{
    r >= 0/1 ? rationals(r - 1/1) : r
}

function rationals_e(r: Rational): Rational
decreases r
{
    //:: ExpectedOutput(termination.failed:tuple.false)
    r >= 0/1 ? rationals_e(r/2) : r
}

//Example decreasing Sequences (Integers)
function sequences(seq:Seq[Int]): Int
decreases seq
ensures result == |seq|
{
    |seq| != 0? 1+sequences(seq[1..]) : 0
}

function sequences_e(seq:Seq[Int]): Int
decreases seq
ensures result == |seq|
{
    //:: ExpectedOutput(termination.failed:tuple.false)
    |seq| != 0 ? sequences_e(seq[1..] ++ Seq(1)) : 0
}


////Example decreasing Sequences (Booleans)
function numberOfUsers(seq:Seq[Bool]): Int
decreases seq
{
    |seq| == 0 ? 0 :
    //:: UnexpectedOutput(termination.failed:tuple.false, /silicon/issue/300/)
        seq[0] ? 1 + numberOfUsers(seq[1..]) : numberOfUsers(seq[1..])
}

//Example decreasing Set
function sets(s:Set[Int]):Int
decreases s
{
    |s|>0 && (|s| in s) ? sets(s setminus Set(|s|)) : 1
}

function sets_e(s:Set[Int]):Int
decreases s
{
    //:: ExpectedOutput(termination.failed:tuple.false)
    |s|>0 && (|s| in s) ? 1 : sets_e(s setminus Set(|s|))
}

//Example decreasing MulitSet
function multiSets(s: Multiset[Int]) : Int
decreases s
{
    |s|>0 && (|s| in s) > 0 ? multiSets(s setminus Multiset(|s|)) : 1
}

function multiSets_e(s: Multiset[Int]) : Int
decreases s
{
    //:: ExpectedOutput(termination.failed:tuple.false)
    |s|>0 && (|s| in s) > 0 ? 1 : multiSets_e(s setminus Multiset(|s|))
}

method otherMultiSetTests()
{
    assert bounded(Multiset(1))
    assert decreasing(Multiset(1),Multiset(1,1))
    assert bounded(Multiset(1,2,3))
    assert decreasing(Multiset(1,2,3), Multiset(1,2,2,3,3))

     //:: ExpectedOutput(assert.failed:assertion.false)
    assert decreasing(Multiset(1,1), Multiset(1,1))

}

method otherMultiSetTests2()
{
    //:: ExpectedOutput(assert.failed:assertion.false)
    assert decreasing(Multiset(2), Multiset(1))
}


field next: Ref
predicate list(xs:Ref)
{
    acc(xs.next) && (xs.next != null ==> list(xs.next))
}

function predicates(xs: Ref): Int
    requires list(xs)
    decreases list(xs)
{
    unfolding list(xs) in xs.next != null ? predicates(xs.next) : 0
}

function predicates_e(xs: Ref): Int
    requires list(xs)
    decreases list(xs)
{
    //:: ExpectedOutput(termination.failed:tuple.false)
    (unfolding list(xs) in xs.next != null) ? predicates_e(xs) : 0
}
//APPENDED PERMISSION MODEL

domain _Perm {
    function unit(): Perm 
    function full(): Perm
    function plus(_x: Perm, _y: Perm): Perm
    function minus(_x: Perm, _y: Perm): Perm
    function geq(_x: Perm, _y: Perm): Bool
    function joinable(_x: Perm, _y: Perm): Bool

    function times(_x: Perm, _y: Perm): Perm

    //Auxiliary functions
    function share(_x: Rational): Perm
    function valid_share(_x: Rational): Bool
    function biConstr(Int, Int): Perm

    //Axioms
    axiom axBiConstr {forall _a: Int, _b: Int :: {biConstr(_a, _b)} biConstr(_a, _b) == share(_a/_b)}


    axiom axValid { forall _x: Rational :: {valid_share(_x)}
        valid_share(_x) <==> (_x >= 0/1 && _x <= 1/1)
    }
    axiom axUnit { unit() == share(0/1) }
    axiom axFull { full() == share(1/1) }
    axiom axInj { forall _a: Rational, _b: Rational :: { share(_a), share(_b) }
        share(_a) == share(_b) ==> _a == _b
    }
    axiom axPlus { forall _a: Rational, _b: Rational :: { plus(share(_a), share(_b)) }
        joinable(share(_a), share(_b)) ==> plus(share(_a), share(_b)) == share(_a + _b)
    }
    axiom axMinus { forall _a: Rational, _b: Rational :: { minus(share(_a), share(_b)) }
        geq(share(_a), share(_b)) ==> minus(share(_a), share(_b)) == share(_a - _b)
    }
    axiom axCanJoin { forall _a: Rational, _b: Rational :: { joinable(share(_a), share(_b)) }
        joinable(share(_a), share(_b)) <==> valid_share(_a) && valid_share(_b) && valid_share(_a + _b)
    }
    axiom axGeq { forall _a: Rational, _b: Rational :: { geq(share(_a), share(_b)) }
        geq(share(_a), share(_b)) <==> valid_share(_a) && valid_share(_b) && valid_share(_a - _b)
    }
    axiom axMul { forall _a: Rational, _b: Rational :: {times(share(_a), share(_b))} times(share(_a), share(_b)) == share(_a * _b)}

    axiom axJoinComm {forall _p: Perm, _q: Perm :: {plus(_p, _q)} plus(_p, _q) == plus(_q, _p)}
    axiom axJoinAssoc {forall _p: Perm, _q: Perm, _r: Perm :: {plus(plus(_p, _q), _r)} plus(plus(_p, _q), _r) == plus(_p, plus(_q, _r))}
    axiom axJoinCancel { forall _p: Perm, _q: Perm, _x: Perm :: {plus(_p, _x), plus(_q, _x)} plus(_p, _x) == plus(_q, _x) ==> _p == _q}

    axiom axUnitGeq { forall _p: Perm :: {geq(_p, unit())} geq(_p, unit())}
    axiom axUnitGeq2 { forall _p: Perm :: {geq(unit(),_p)} geq(unit(),_p) <==> _p == unit()}
    axiom axFullGeq { forall _p: Perm :: {geq(full(),_p)} geq(full(),_p)}
    axiom axFullGeq2 { forall _p: Perm :: {geq(_p, full())} geq(_p, full()) <==> _p == full()}

    axiom axUnitJoin { forall _p: Perm :: {plus(unit(),_p)} plus(unit(),_p) == _p}
    axiom axUnitJoinable { forall _p: Perm :: {joinable(_p, unit())} joinable(_p, unit())}
    axiom axJoinableComm { forall _p: Perm, _q: Perm :: {joinable(_p, _q)} joinable(_p, _q) == joinable(_q,_p)}
    axiom axTimes0 { forall _p: Perm :: {times(unit(),_p)} times(unit(),_p) == unit()}
    axiom axTimes1 { forall _p: Perm :: {times(full(),_p)} times(unit(),_p) == _p}

    axiom axGeqPlus { forall _a: Perm, _x: Perm, _y: Perm :: {geq(_a, plus(_x, _y))} geq(_a, plus(_x, _y)) ==> geq(_a, _x)}
    axiom axGeqMinus {forall _a: Perm, _x: Perm, _y: Perm :: {geq(_a, plus(_x, _y))} geq(_a, plus(_x, _y)) ==> geq(minus(_a, _y), _x)}
    axiom axMinusUneq { forall _p: Perm, _q: Perm :: {geq(_p, _q)} geq(_p, _q) && _p != _q ==> minus(_p, _q) != unit()}

}
